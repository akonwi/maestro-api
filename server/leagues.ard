use ard/decode
use ard/sql

struct League {
  id: Int,
  name: Str,
  code: Str,
}

fn decode_league(data: Dynamic) League![decode::Error] {
  let id = try decode::run(data, decode::field("id", decode::int))
  let name = try decode::run(data, decode::field("name", decode::string))
  let code = try decode::run(data, decode::field("code", decode::string))

  Result::ok(League{
    id: id,
    name: name,
    code: code,
  })
}

fn get_all(db: sql::Connection) [League]!Str {
  let rows = try db.query("select * from leagues order by name asc").all(["_": ()])
  mut leagues: [League] = []
  for row in rows {
    let league = try decode_league(row) -> errs {
      Result::err(errs.at(0).to_str())
    }
    leagues.push(league)
  }
  Result::ok(leagues)
}

// a Set would be great
fn get_ignored(db: sql::Connection) [Int:Bool] {
  mut ignored: [Int:Bool] = [:]
  let rows = try db.query("select id from ignored_league").all(["": ()]) -> _ { ignored }

  for row in rows {
    match decode::run(row, decode::field("id", decode::int)) {
      ok(id) => ignored.set(id, true),
      err => ()
    }
  }
  ignored
}

use ard/async
use ard/env
use ard/io
use ard/http
use ard/json

use maestro/bets
use maestro/data
use maestro/db
use maestro/leagues
use maestro/matches
use maestro/teams

let res_headers = [
  "Access-Control-Allow-Origin": "*",
  "Access-Control-Allow-Methods": "GET, POST, PUT, PATCH, DELETE",
  "Access-Control-Allow-Headers": "Content-Type",
  "Content-Type": "application/json"
]

fn internal_error(msg: Str) http::Response {
  // todo: need copy semantics so the map can be copied and then mutated
  // i.e. `res.headers.drop("Content-Type")`
  http::Response{
    status:500,
    body: msg,
    headers: [
      "Access-Control-Allow-Origin": "*",
      "Access-Control-Allow-Methods": "GET, POST, PUT, PATCH, DELETE",
      "Access-Control-Allow-Headers": "Content-Type",
    ],
  }
}

let not_found = http::Response{status:404, body:"Not found", headers:res_headers}

fn main() {
  let conn = db::init()

  // start fiber for updating db regularly
  async::start(fn() { data::sync() })

  let env_port = env::get("PORT").or("")
  if env_port.is_empty() {
    panic("Missing PORT environment variable")
  }

  let port = Int::from_str(env_port).or(3000)

  io::print("starting on port {port}")

  http::serve(
    port: port,
    handlers: [
      "/bets/overview": fn(req: http::Request) http::Response {
        match req.method {
          http::Method::Get => {
            let overview = try bets::get_overview(conn) -> internal_error
            let teams_by_id = try teams::get_all(conn) -> internal_error

            struct Res { overview: bets::Overview, teams: [Int:teams::Team] }
            match json::encode(Res{ overview:overview, teams:teams_by_id }) {
              ok(body) => http::Response{status:200, body:body, headers:res_headers},
              err => internal_error(err)
            }
          },
           _ => not_found
        }
      },

      "/leagues": fn(req: http::Request) http::Response {
        match req.method {
          http::Method::Get => {
            let data = try leagues::get_all(conn) -> internal_error

            struct Res { leagues: [leagues::League] }
            match json::encode(Res{ leagues:data }) {
              ok(body) => http::Response{status:200, body:body, headers:res_headers},
              err => internal_error(err)
            }
          },
           _ => not_found
        }
      },

      "/leagues/:id/matches": fn(req: http::Request) http::Response {
        match req.method {
          http::Method::Get => {
            let league_id = Int::from_str(req.path_param("id")).or(-1)
            let fixtures = try matches::get_league(conn, league_id) -> internal_error

            struct Res { matches: [matches::Match] }
            match json::encode(Res{ matches:fixtures }) {
              ok(body) => http::Response{status:200, body:body, headers:res_headers},
              err => internal_error(err)
            }
          },
           _ => not_found
        }
      },
    ],
  )
}
